plugins {
  id 'org.springframework.boot' version '2.6.6'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'info.solidsoft.pitest' version '1.7.0'
  id 'org.owasp.dependencycheck' version '6.2.2'
  id 'com.google.cloud.tools.jib' version '3.2.1'
}

def main_class = 'it.francescofiora.tasks.taskapi.Application'

def springdoc_openapi_version = parent.property('springdoc-openapi.version')
def mapstruct_version = parent.property('mapstruct.version')
def lombok_version = parent.property('lombok.version')
def logback_version = parent.property('logback.version')
def openpojo_version = parent.property('openpojo.version')
def apache_httpclient_version = parent.property('apache-httpclient.version')
def mongo_version = parent.property('mongo.version')

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
  implementation 'org.springframework.boot:spring-boot-starter-activemq'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'

  implementation "org.springdoc:springdoc-openapi-ui:${springdoc_openapi_version}"
  implementation "org.springdoc:springdoc-openapi-data-rest:${springdoc_openapi_version}"

  implementation "org.jolokia:jolokia-core"

  implementation 'org.messaginghub:pooled-jms'

  implementation "org.projectlombok:lombok:${lombok_version}"
  implementation "org.mapstruct:mapstruct:${mapstruct_version}"

  annotationProcessor "org.projectlombok:lombok:${lombok_version}"
  annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct_version}"

  implementation project(':task-message')

  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation "de.flapdoodle.embed:de.flapdoodle.embed.mongo:${mongo_version}"
  testImplementation "com.openpojo:openpojo:$openpojo_version"
  testImplementation "org.projectlombok:lombok:${lombok_version}"
  testImplementation "org.apache.httpcomponents:httpclient:${apache_httpclient_version}"

  testAnnotationProcessor "org.projectlombok:lombok:${lombok_version}"
}

springBoot {
  mainClass.set("${main_class}")
  buildInfo()
}

jib {
	container {
		mainClass = "${main_class}"
	}
	from {
	  image = 'azul/zulu-openjdk-alpine:17'
	}
	to {
	  image = 'francescofiora-task-api'
	  tags = ['latest']
	}
	container {
	  creationTime = 'USE_CURRENT_TIMESTAMP'
	  workingDirectory = '/workspace'
	}
}

eclipse {
  sourceSets {
     main {
         java {
             srcDirs += ["build/generated/sources/annotationProcessor/java/main"]
         }
     }
  }
}

pitest {
  timestampedReports = false
  junit5PluginVersion = '0.15'
  excludedTestClasses = ['it.francescofiora.tasks.taskapi.jms.JmsConsumerTest']
}

dependencyCheck {
  suppressionFile = rootProject.file('config/suppression.xml')
  analyzers {
    assemblyEnabled = false
  }
  formats = [ "HTML" ]
}
