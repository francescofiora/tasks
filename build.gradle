plugins {
  id 'java'
  id 'eclipse-wtp'
  id 'org.sonarqube' version '3.3'
}

group 'it.francescofiora'
version = '1.0-SNAPSHOT'

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
  }
  sourceCompatibility = 1.11
  targetCompatibility = 1.11
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse-wtp'
  apply plugin: 'checkstyle'
  apply plugin: 'jacoco'

  sourceCompatibility = 1.11
  targetCompatibility = 1.11

  group = rootProject.group
  version = rootProject.version
    
  checkstyle {
    toolVersion property('checkstyle.version')
    configFile  rootProject.file("config/checkstyle/checkstyle.xml")
    configProperties = [
         'checkstyle.cache.file': "${buildDir}/checkstyle.cache",
    ]
  }

  tasks.withType(Checkstyle) {
    reports {
      xml.enabled = false
      html.enabled = true
    }
  }
  checkstyleMain {
    source ='src/main/java'
  }

  test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
  }

  jacocoTestReport {
    dependsOn test
    reports {
      xml.enabled true
      csv.enabled false
      html.destination file("${buildDir}/reports/jacoco/test")
      xml.destination  file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
  }

  sonarqube {
    properties {
      property "sonar.sourceEncoding", "UTF-8"
      property 'sonar.core.codeCoveragePlugin', 'jacoco'
      property 'sonar.coverage.jacoco.xmlReportPaths', "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
  }

  task cleanResources(type: Delete) {
    delete "build/resources"
  }
}

wrapper {
  gradleVersion = "7.0"
}
